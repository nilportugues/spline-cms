def projects = [
		[
				directory : '',
				prefix : '',
				projects : ['spline-api-web', 'spline-api-core','spline-core']
		],
		[
				directory : 'spline-entity',
				prefix : '',
				projects : ['repository','relationship','type']
		],
        [
                directory: 'spline-query',
                prefix: '',
                projects:['query-core', 'query-neo4j']
        ]
]

projects.each {group ->
	group.projects.each {project ->
		def fileBaseName = project.replace(group.prefix, '').replaceAll("\\p{Upper}") { "-${it.toLowerCase()}" }
		def projectDirName = "${group.directory}/$fileBaseName"
		def projectDir = new File(settingsDir, projectDirName)
		def buildFileName = "${fileBaseName}.gradle"
		def projectDescriptor = createProjectDescriptor(rootProject, project, projectDir)
		projectDescriptor.setBuildFileName(buildFileName)
		rootProject.children.add(projectDescriptor)
	}
}

rootProject.name = 'spline-cms'
rootProject.children.each { project ->
	assert project.projectDir.isDirectory()
	assert project.buildFile.isFile()
}
