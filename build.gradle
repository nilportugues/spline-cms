ext {
	gradleVersion = '2.9'
	javaVersion = '1.8'
	splineVersion = '0.1'
}

buildscript {
	ext {
		springBootVersion = '1.2.5.RELEASE'
		dependencyManagementVersion = '0.5.4.RELEASE'
	}
	repositories {
		mavenLocal()
		maven {
			url 'http://maven.springframework.org/release'
		}
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementVersion"
	}
}

configure(allprojects) {
	repositories {
		maven {
			url 'http://maven.springframework.org/release'
		}
		mavenCentral()
		jcenter()
	}
	group='se.spline'
	version=splineVersion
}

configure(subprojects) {
	apply plugin: 'java'

	compileJava {
		sourceCompatibility = javaVersion
		targetCompatibility = javaVersion
	}

	compileTestJava {
		sourceCompatibility = javaVersion
		targetCompatibility = javaVersion
	}

    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:2.0.1.RELEASE'
        }
        dependencies {
            dependencySet (group: 'org.axonframework', version: '2.4.3') {
                entry 'axon-core'
                entry 'axon-test'
                entry 'axon-mongo'
            }
            dependencySet (group: 'io.springfox', version: '2.3.0') {
                entry 'springfox-swagger2'
                entry 'springfox-swagger-ui'
            }
        }
    }
}

task wrapper(type: Wrapper) {
	gradleVersion = "${project.ext.gradleVersion}"
}


